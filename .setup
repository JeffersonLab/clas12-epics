#
# File: .setup
#
# EPICS Environment
#

setenv EPICS_VER 3.14.12.5
setenv EPICS_BASE /usr/clas12/R$EPICS_VER/base
setenv EPICS_EXTENSIONS /usr/clas12/R$EPICS_VER/extensions

setenv MIBDIRS /usr/clas12/R$EPICS_VER/modules/snmp-nscl-1-0-RC9/mibs:/usr/share/snmp/mibs
setenv MIBS ALL

# set EPICS host architecture
setenv OSTYPE `uname`
setenv MACHINE `uname -m`
if ( ($OSTYPE == "Linux") && ($MACHINE == "i686" || $MACHINE == "x86") ) then
  setenv EPICS_HOST_ARCH linux-x86
else
  setenv EPICS_HOST_ARCH linux-x86_64
endif

## caget/caput/etc, medm/striptool/etc...
set -f path=("$EPICS_BASE/bin/$EPICS_HOST_ARCH" $path:q)
set -f path=("$EPICS_EXTENSIONS/bin/$EPICS_HOST_ARCH" $path:q)
## EPICS tools and utils
set -f path=("/usr/clas12/release/pro/epics/bin" $path:q)
## CSS, alarm tools, etc
set -f path=("/usr/clas12/css/pro/$EPICS_HOST_ARCH/bin" $path:q)
## acctools rsync'd from Cebaf (MyaViewer, etc)
set -f path=("/usr/csite/certified/bin" $path:q)

if ( ! $?LD_LIBRARY_PATH ) setenv LD_LIBRARY_PATH 
setenv LD_LIBRARY_PATH "$LD_LIBRARY_PATH":"$EPICS_BASE/lib/$EPICS_HOST_ARCH":"$EPICS_EXTENSIONS/lib/$EPICS_HOST_ARCH"

setenv EPICS_CA_AUTO_ADDR_LIST no
setenv EPICS_CA_ADDR_LIST "129.57.255.4 129.57.163.255 129.57.57.188 129.57.99.255 129.57.191.255 129.57.68.97 129.57.86.16 129.57.86.29 129.57.86.37 129.57.160.46 129.57.160.54"

#find the directory of this sourced .setup file and set up relevant path and python path
#let's see how robust this is

# this creates nuisance warnings:
set this=`/usr/sbin/lsof +p $$ | grep ".setup" | awk '{print $9}'`

set clas12epics=`dirname ${this}`
set script_dir="${clas12epics}/apps/scripts"
set css_script_dir="${clas12epics}/css_share/common/scripts"
setenv CSS_SHARE_DIR "${clas12epics}/css_share"


set -f path=($path:q "${script_dir}")

if ( ! $?PYTHONPATH ) then
   setenv PYTHONPATH "${script_dir}:${css_script_dir}:/usr/clas12/third-party-libs/pyepics-RHEL7"
else
   setenv PYTHONPATH "${PYTHONPATH}:${script_dir}:${css_script_dir}:/usr/clas12/third-party-libs/pyepics-RHEL7"
endif

setenv EPICS_SCRIPTS "${script_dir}"
